apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'android-aspectjx'//面向切面
apply plugin: 'com.alibaba.arouter'

aspectjx {
    exclude 'androidx.*'
}

static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.shetj.diyalbume"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        flavorDimensions "versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }
    }

    dataBinding {
        enabled = true
    }


    kapt {
        arguments {
            arg("AROUTER_MODULE_NAME", project.getName())
        }
    }
    signingConfigs {
        release {
            keyAlias 'shetj'
            keyPassword '123456'
            storeFile file('./test.jks')
            storePassword '123456'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
    }

    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "USE_CANARY", "false"
            debuggable false
            minifyEnabled false
            zipAlignEnabled true
            // dex突破65535的限制
            multiDexEnabled true
            // 移除无用的resource文件
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "false"
            debuggable true
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            // dex突破65535的限制
            multiDexEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            ndk {
                abiFilters 'x86'                 // for debug in AVD.
            }
        }
    }

//    applicationVariants.all { variant ->
//        variant.outputs.all { output ->
//            def outputFile = output.outputFile
//            if (outputFile != null && outputFile.name.endsWith('.apk')) {
//                def fileName = outputFile.name.replace(".apk", "-${releaseTime()}-${defaultConfig.versionName}.apk")
//                outputFileName = fileName
//            }
//        }
//    }

//    //打包重命名
    applicationVariants.all {
        variant ->
            variant.outputs.all {
                output -> output.outputFileName = "shetj-${variant.name}-${releaseTime()}-${defaultConfig.versionName}.apk"
            }
    }

    //导入aar
    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    def listSubFile = {
        def resFolder = 'src/main/res/layouts'
        def files = file(resFolder).listFiles()
        def folders = []
        files.each {
            item -> folders.add(item.absolutePath)
        }
        folders.add(file(resFolder).parentFile.absolutePath)
        return folders
    }
    sourceSets {
        main {
            res.srcDirs = listSubFile()
        }
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.google.code.gson') {
                details.useVersion '2.8.2'
            }
            if (requested.group == 'com.github.bumptech.glide') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '4.9.0'
                }
            }
            if (requested.group == 'io.reactivex.rxjava2') {
                if (!requested.name.startsWith("multidex")) {
                    if (requested.name.startsWith("rxjava")) {
                        details.useVersion '2.2.13'
                    }
                    if (requested.name.startsWith("rxandroid")) {
                        details.useVersion '2.1.1'
                    }
                }
            }
            if (requested.group == 'com.tencent.mm.opensdk') {
                details.useVersion '5.1.4'
            }
            if (requested.group == 'com.squareup.okhttp3') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion '3.10.0'
                }
            }
            //确保只有一个base
            if (requested.group == 'com.github.SheTieJun') {
                if (requested.name.startsWith("Base")) {
                    details.useVersion "$base_version"
                }
            }
            if (requested.group == 'androidx.annotation') {
                if (requested.name.startsWith("annotation")) {
                    details.useVersion "1.1.0"
                }
            }
            if (requested.group == 'androidx.core') {
                details.useVersion "$core_ktx"
            }
            if (requested.group == 'androidx.constraintlayout') {
                if (requested.name.startsWith("constraintlayout")) {
                    details.useVersion "$constraintlayout"
                }
            }
        }
    }


}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "androidx.core:core-ktx:$core_ktx"
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout"
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0' //liveData kt
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0' //viewModel kt


    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.browser:browser:1.2.0'


    implementation 'androidx.palette:palette:1.0.0'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'


    implementation 'com.shuyu:GSYVideoPlayer:7.1.2' //https://github.com/CarGuo/GSYVideoPlayer
    implementation 'com.devyok.ipc:ipc-libcore:0.0.2'
    implementation 'com.github.bumptech.glide:glide:4.10.0'
    kapt 'com.github.bumptech.glide:compiler:4.10.0'
    kapt 'com.alibaba:arouter-compiler:1.2.1'

    implementation 'com.airbnb.android:lottie:3.0.7'
    implementation 'com.clj.fastble:FastBleLib:2.3.2'

    implementation project(path: ':rxFingerPrinter')
    implementation project(':transition')
    implementation project(':tencent-x5')
    implementation project(':update')
    implementation project(':fresco')
    implementation project(':bdmap')
    implementation project(':router')
    implementation project(':download')


    implementation "com.github.SheTieJun:Base:$base_version"
    implementation 'com.github.SheTieJun:aspectutils:12004150ad'
    def db_version = "a5020e0ed4"  //commit 前10位
    implementation "com.github.SheTieJun:SimXutilsDb:$db_version"
    implementation "com.github.SheTieJun:OrangeDialog:0.0.1"


    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:5.1.4'
    implementation 'com.youth.banner:banner:1.4.10'
    implementation 'com.irozon.sneaker:sneaker:1.0.3'

    implementation 'com.github.iielse:ImageWatcher:1.1.5'
    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.2.2'

    implementation 'com.afollestad.material-dialogs:core:3.1.0'

    implementation 'com.zhouyou:rxeasyhttp:2.1.5'

    implementation 'com.github.ybq:Android-SpinKit:1.2.0'

    def work_version = "2.3.3"
    // (Java only)
    implementation "androidx.work:work-runtime:$work_version"
    // Kotlin + coroutines
    // optional - RxJava2 support
    implementation "androidx.work:work-rxjava2:$work_version"
    // optional - Test helpers
    androidTestImplementation "androidx.work:work-testing:$work_version"
    //KTX
    implementation "androidx.work:work-runtime-ktx:$work_version"


    def nav_version = "1.0.0"
    implementation "android.arch.navigation:navigation-fragment:$nav_version" // use -ktx for Kotlin
    implementation "android.arch.navigation:navigation-ui:$nav_version" // use -ktx for Kotlin


    def camerax_version = "1.0.0-alpha06"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-view:1.0.0-alpha03"
    implementation "androidx.camera:camera-extensions:1.0.0-alpha03"

    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation 'androidx.customview:customview:1.1.0-alpha02' //ViewDragHelper
    //MarkDown 空间
    implementation 'us.feras.mdv:markdownview:1.1.0'

    //音视频
    implementation 'com.github.microshow:RxFFmpeg:2.1.0'
    implementation project(path: ':mvvm')
    implementation project(path: ':constraintlayout')

    //搭建本地服务器(https://github.com/koush/AndroidAsync)
    implementation 'com.koushikdutta.async:androidasync:2.2.1'
    implementation project(path: ':cling')

}
